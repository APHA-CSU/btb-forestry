process JSON_EXPORT {
    errorStrategy 'ignore'
    tag "$clade"
    publishDir "$params.publishDir/jsonExport/", mode: 'copy'
    
    input:
        tuple val (clade), path (MP_rooted), path (phylo), path (metadata), path (locations)
        path config
        path colours
        val today
    
    output:
        tuple val (clade), path ("*.json")
    
    script:
    """
    #!/bin/bash
    set -eo pipefail
    
    export AUGUR_RECURSION_LIMIT=10000

    # This process collects the relavant information and exports in json format for
    # direct visualization using Auspice: 
    # https://docs.nextstrain.org/projects/augur/en/stable/usage/cli/export.html
    # The general format of .json files generated by augur is given here: 
    # https://docs.nextstrain.org/projects/augur/en/stable/usage/json_format.html

    augur export v2 -t $MP_rooted \
                --metadata $metadata \
                --node-data $phylo \
                --auspice-config $config \
                --lat-longs $locations \
                --panels tree map \
                --colors $colours \
                --output ${clade}.json

    sed -i 's/"SNP Distance": 0//g' ${clade}.json
    """
}