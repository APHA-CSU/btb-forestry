process REFINE_TREES {
    errorStrategy 'ignore'
    tag "$clade"
    publishDir "$params.publishDir/augurTrees/", mode: 'copy'

    input:
        tuple val (clade), path (MP_nwk), val (maxN), val (outGroup), val (outGroupLoc), path (snp_only)
        val (today)

    output:
        tuple val (clade), path ("*_MP-rooted.nwk"), path ("*_phylo.json")

    script:
    """
    #!/bin/bash
    set -eo pipefail

    # This process roots the tree using the outgroup and exports branch lengths in
    # json format: 
    # https://docs.nextstrain.org/projects/augur/en/stable/usage/cli/refine.html
    # The general format of .json files generated by augur is given here: 
    # https://docs.nextstrain.org/projects/augur/en/stable/usage/json_format.html

    augur refine -t ${MP_nwk} \
                --root ${outGroup} \
                --output-tree ${clade}_${today}_MP-rooted.nwk \
                --output-node-data ${clade}_${today}_phylo.json \
                --branch-length-inference input
    """
}